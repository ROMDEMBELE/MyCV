plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
}

apply from: project.lintAndroid
apply from: project.detekt

def getVersionCode = { ->
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'log', '-1', '--format=%ct'
            standardOutput = code
        }
        return Integer.parseInt(code.toString().trim())
    }
    catch (ignored) {
        return -1
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null
    }
}

android {
    compileSdkVersion project.compileSdkVersion
    defaultConfig {
        applicationId "com.diabeloop.architecturesample"
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode getVersionCode()
        versionName getVersionName()
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    flavorDimensions "branch"

    productFlavors {

        mock {
            dimension "branch"
        }

        external {
            dimension "branch"
        }

    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        if (names.contains("mock")
                && buildType.name == "release") {
            ignore = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation project(":domain")
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    externalImplementation project(":data")
    mockImplementation project(":data-mock")

    implementation "androidx.appcompat:appcompat:_"

    implementation "com.google.android.material:material:_"

    implementation "com.google.dagger:hilt-android:_"
    kapt("com.google.dagger:hilt-android-compiler:_")

    implementation "androidx.navigation:navigation-fragment-ktx:_"
    implementation "androidx.navigation:navigation-ui-ktx:_"
}
